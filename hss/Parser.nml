/*
 *  Hss Format
 *  Copyright (c)2008 Nicolas Cannasse
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2.1 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License or the LICENSE file for more details.
 */
open Lexer;
open Hss.Ast;

type error_msg {
	Unexpected : token;
	Message : string;
	Class_expected;
	Expected_semicolon : token;
}

exception Error : (error_msg , pos)

function error_msg(m) {
	match m {
	| Unexpected t -> "Unexpected " + s_token t
	| Class_expected -> "A node class is expected here"
	| Expected_semicolon t -> "Unexpected '" + s_token t + "' maybe missing ;"
	| Message s -> s
	}
}

function error(m,p) {
	throw Error(m,p)
}

function mk(t,p) {
	(t,p)
}

function semicolon(s) {
	match s {
	| [< (Semicolon,p) >] -> p
	| [< (tk,p) >] -> error (Expected_semicolon tk) p
	}
}

function rec program(s) {
	match s {
	| [< (Eof,_) >] -> []
	| [< e = expr s; p = program s >] -> e :: p
	}
}

function rec expr(s) {
	match s {
	| [< (Keyword Var,p1); (Const (Ident i),_); (Eq,_) >] ->
		match s {
		| [< (BraceOpen,p2); l = expr_list s >] -> (EVarBlock i l, punion p1 p2)
		| [< v = value s; p2 = semicolon s >] -> (EVar i v, punion p1 p2)
		}
	| [< (Const (Ident "@include"),p1); (ParentOpen,_); (Const (String s),_); (ParentClose,p2) >] -> (EInclude s, punion p1 p2)
	| [< (Const (Ident "@import" ),p1); (ParentOpen,_); (Const (String f),_); (ParentClose,p2) >] -> (EImport  f, punion p1 p2)
	| [< (Const (Ident "@keyframes" ),_); (Const (Ident n),p); (BraceOpen,_); l = keyframes s; >] -> (EKeyframes n l, p)
	| [< (Media str,p1); (BraceOpen,_); l = expr_list s; >] -> (EMedia str l, p1)
	| [< c = class_list s true; (BraceOpen,p); l = expr_list s; >] -> (EBlock c l, p)
	}
}

function rec keyframes(s) {
	match s {
	| [< (BraceClose,_) >] -> []
	| [< (Const (Ident str), p1); (BraceOpen,_); >] ->
		((VString str, p1), expr_list s) :: keyframes s
	| [< (Const (Int i), p1); (Percent, p2); (BraceOpen,_); >] ->
		((VUnit(1.0 * i, "%"), punion p1 p2), expr_list s) :: keyframes s
	| [< (Keyword Var,p); >] -> error(Message "Defining variables is not allowed here", p)
	| [< (v, p) >] -> error(Unexpected v, p)
	}
}

function rec expr_list(s) {
	match s {
	| [< (BraceClose,_) >] ->
		[]
	| [< (Const (Ident att),p); (DoubleDot,_); v = value s; _ = semicolon s >] ->
		(EAttribute att v,p) :: expr_list s
	| [< (Const (Val v),p); _ = semicolon s >] ->
		(EUseBlock v,p) :: expr_list s
	| [< e = expr s >] ->
		e :: expr_list s
	}
}

function rec value(s) {
	match s {
	| [< (Sharp,p) >] -> value_next (value_hex p s) s
	| [< (Const (Ident i),p1) >] ->
		match s {
		| [< (Eq,_); v = value s >] ->
			function bind(v) {
				(VBind i v,punion p1 (pos v))
			}
			match v {
			| (VList (v :: l),p2) -> (VList (bind v :: l), punion p1 p2)
			| (VGroup (v :: l),p2) -> (VGroup (bind v :: l), punion p1 p2)
			| _ -> bind v
			}
		| [< >] ->
			value_next (VIdent i,p1) s
		}
	| [< (Const (String str),p) >] -> value_next (VString str,p) s
	| [< (Const (Int i),p) >] -> value_unit (i * 1.0) (VInt i, p) s
	| [< (Const (Float f),p) >] -> value_unit f (VFloat f, p) s
	| [< (Const (Val v),p) >] -> value_next (VVar v,p) s
	}
}

function rec value_unit(f,v,s) {
	var p1 = pos v
	var p2 = pos (stream_token s 0)
	var v = if (p1.pmax == p2.pmin && p1.pline == p2.pline) then
		match s {
		| [< (Const (Ident i),_) >] -> (VUnit f i, punion p1 p2)
		| [< (Percent,_) >] -> (VUnit f "%", punion p1 p2)
		| [< >] -> v
		}
	else
		v;
	value_next v s
}

function rec value_next(v,s) {
	match s {
	| [< (ParentOpen,_); p = value s; (ParentClose,p2) >] ->
		var args = match p {
		| (VList l,_) -> l
		| _ -> [p]
		}
		value_next (VCall v args, punion (pos v) p2) s
	| [< (Exclamation,p1); (Const (Ident i),p2) >] ->
		(VLabel i v,punion p1 p2)
	| [< v2 = value s >] ->
		function rec loop(v2) {
			var p = punion (pos v) (pos v2);
			match fst v2 {
			| VGroup l -> (VGroup (v :: l), p)
			| VList (v2 :: l) -> (VList (loop v2 :: l), p)
			| VLabel (l,v2) -> (VLabel l (loop v2), p)
			| _ -> (VGroup [v;v2], p)
			}
		}
		loop v2
	| [< (Comma,_); v2 = value s >] ->
		function rec loop(v2) {
			var p = punion (pos v) (pos v2);
			match fst v2 {
			| VList l -> (VList (v :: l), p)
			| VLabel (l,v2) -> (VLabel l (loop v2), p)
			| _ -> (VList [v;v2], p)
			}
		}
		loop v2
	| [< (Op o,_); v2 = value s >] ->
		function rec loop(o, v, v2) {
			var p = punion (pos v) (pos v2);
			match fst v2 {
			| VOp(o1, v1, v2) -> (VOp o1 (loop o v v1) v2, p)
			| _ -> (VOp o v v2, p)
			}
		}
		var p = punion (pos v) (pos v2);
		match fst v2 {
		| VList (v2 :: l) -> (VList  ((loop o v v2) :: l), p)
		| VGroup(v2 :: l) -> (VGroup ((loop o v v2) :: l), p)
		| _-> loop o v v2
		}
	| [< >] ->
		v
	}
}

function rec class_list(s,first) {
	match s {
	| [< c = class s first >] ->
		match s {
		| [< (Comma,_) >] -> c :: class_list s false
		| [< >] -> [c]
		}
	}
}

function rec parse_attr_value(s) {
	match s {
	| [< (Const (Ident v),_) >] -> v
	| [< (Const (String s),_) >] -> s
	}
}

function rec parse_attribute(s) {
	match s {
	| [< (BracketClose,p) >] -> (p, AExists)
	| [< (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, AEqual v)
	| [< (Tild,_); (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, AContain v)
	| [< (Op Or,_); (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, ADash v)
	| [< (Op Xor,_); (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, ABegin v)
	| [< (Dollar,_); (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, AEnd v)
	| [< (Op Mul,_); (Eq,_); v = parse_attr_value s; (BracketClose,p) >] -> (p, ASubstr v)
	}
}

function rec value_hex(p,s) {
	var str = &"";
	var pos = &p;
	var hex = Regexp.build "^[A-Za-z0-9]+$";
	function rec loop(i) {
		var t = stream_token s i;
		var p = *pos;
		var p2 = snd t;
		pos := p2;
		// contiguous
		if p.pline != p2.pline || p.pmax != p2.pmin then
			i
		else match fst t {
		| Const (Ident id) when Regexp.find hex id 0 String.length(id) ->
			str := *str + id;
			loop (i+1)
		| Const (Int k) when k >= 0 ->
			str := *str + string k;
			loop (i+1)
		| _ ->
			i
		}
	}
	var n = loop 0;
	if n == 0 then error (Message "Expected hexadecimal value here") p;
	stream_junk s n;
	(VHex (*str), punion p (*pos))
}

function rec class(s,first) {
	var c = { node = None; id = None; classes = []; sub = None; selector = []; attributes = []; operator = OpDefault; };
	var pos = &null_pos;
	var last = &None;
	function rec loop(i) {
		var t = stream_token s i;
		var p = *pos;
		var p2 = snd t;
		pos := p2;
		// contiguous
		if (i > 0 || c.attributes != []) && (p.pline != p2.pline || p.pmax != p2.pmin) then
			i
		else match fst t {
			| Const (Ident id) ->
				match *last {
				| None when i == 0 ->
					c.node := Some id;
					loop (i+1)
				| Some Sharp ->
					last := None;
					c.id := Some id;
					loop (i+1)
				| Some Dot ->
					last := None;
					c.classes := List.append c.classes [id];
					loop (i+1)
				| Some DoubleDot ->
					last := None;
					c.selector:= (PSelector id)::c.selector;
					loop (i+1)
				| Some DbDoubleDot ->
					last := None;
					c.selector:= (PDbSelector id)::c.selector;
					loop (i+1)
				| _ ->
					i
				}
			| Op Mul when *last == None && i == 0 ->
				c.node := Some "*";
				loop (i+1)
			| Sharp when *last == None && c.id == None ->
				last := Some Sharp;
				loop (i+1)
			| Dot when *last == None ->
				last := Some Dot;
				loop (i+1)
			| DoubleDot when *last == None ->
				last := Some DoubleDot;
				loop (i+1)
			| DbDoubleDot when *last == None ->
				last := Some DbDoubleDot;
				loop (i+1)
			| BracketOpen when *last == None ->
				match fst (stream_token s (i+1)) {
				| Const (Ident att) ->
					stream_junk s (i + 2);
					var p, op = parse_attribute s;
					c.attributes := (att, op) :: c.attributes;
					pos := p;
					loop 0
				| _ ->
					i+1
				}
			| Selector (id,s) ->
				match *last {
				| None when i == 0 ->
					c.node := Some id;
					c.selector:= (PSelector s)::c.selector;
					loop (i+1)
				| Some Sharp ->
					last := None;
					c.id := Some id;
					c.selector:= (PSelector s)::c.selector;
					loop (i+1)
				| Some Dot ->
					last := None;
					c.classes := List.append c.classes [id];
					c.selector:= (PSelector s)::c.selector;
					loop (i+1)
				| Some DoubleDot ->
					last := None;
					c.selector:= (PSelector s)::(PSelector id)::c.selector;
					loop (i+1)
				| _ ->
					i
				}
			| SelectorCall (s,e) ->
				last := None;
				c.selector := PSelectorCall(s, e)::c.selector;
				pos := {psource = p2.psource; pline = p2.pline; pmin = p2.pmin; pmax = p2.pmax + 1; };
				loop (i + 2)
			| ParentOpen when i == 1 && c.node == Some "CSS" ->
				match (stream_token s (i + 1), stream_token s (i + 2)) {
				| ((Const (String s),_), (ParentClose,p)) ->
					c.node := Some s;
					pos := p;
					loop (i + 3)
				| _ -> i
				}
			| _ ->
				i
		}
	}
	match s {
	| [< (Gt,_) >] -> c.operator := OpChild
	| [< (Tild,_) >] -> c.operator := OpPreceding false
	| [< (Op Add,_) >] -> c.operator := OpPreceding true
	| [< (Op And,p) >] ->
		match fst (stream_token s 0) {
		| Dot | DoubleDot | DbDoubleDot | BracketOpen | SelectorCall _ -> c.operator := OpJoint
		| _ -> error Unexpected(Op And) p
		}
	| [< >] -> ()
	}
	var n = loop 0;
	if n == 0 && c.attributes == [] then error (if first then Unexpected fst(stream_token s 0) else Class_expected) (*pos);
	c.attributes := List.rev c.attributes;
	stream_junk s n;
	match *last {
	| None -> ()
	| Some t -> error (Unexpected t) (*pos);
	}
	try
		c.sub := Some (class s false)
	catch {
		Error (Class_expected,_) -> ()
	}
	c
}

function parse(lexer) {
	var last = &(Eof,null_pos);
	function rec next_token() {
		var t = Lexer.token lexer (*Hss.Lexer.expr);
		match fst t {
		| Comment s | CommentLine s ->
			next_token()
		| _ ->
			last := t;
			t
		}
	}
	try {
		program (stream next_token)
	} catch {
		Stream_error -> error Unexpected(fst(*last)) snd(*last)
	}
}
